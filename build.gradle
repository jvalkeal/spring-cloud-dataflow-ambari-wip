buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.netflix.nebula:gradle-ospackage-plugin:2.2.6'
	}
}

if (tasks.findByPath("artifactoryPublish")) {
	artifactory {
		publish {
			repository {
				ivy {
					artifactLayout = '[organization]/[revision]/[organization]-plugin-[module]-[revision]-1.noarch.[ext]'
				}
			}
		}
	}
}

configure(allprojects) {
	group = 'scdf'
}

configure(subprojects) { subproject ->
	apply plugin: 'os-package'
	apply plugin: 'rpm'
	buildDeb.enabled = false

	ospackage {
		release = 1
		arch = NOARCH
		os = LINUX
		user 'root'
		permissionGroup = 'root'
		packageDescription = 'Spring Cloud Dataflow plugin contains Ambari\'s stack definition for Spring Cloud Dataflow service.'
		packageGroup = 'Applications/Databases'
		license = 'Apache License v2.0'
		requires('ambari-server', '1.7', GREATER | EQUAL)
		requires('python', '2.6', GREATER | EQUAL)
		postInstall file("${rootProject.projectDir}/src/main/package/rpm/postinstall.sh")
		preUninstall file("${rootProject.projectDir}/src/main/package/rpm/preremove.sh")
	}

	artifacts {
		archives buildRpm
	}
}

project('hdp') {
	if (tasks.findByPath("artifactoryPublish")) {
		artifactoryPublish {
			properties = ['qa.level': 'basic', 'q.os': 'win32, deb, osx']
		}
	}
	buildRpm {
		packageName = 'scdf-plugin-hdp'
		from("${rootProject.projectDir}/src/main/resources/services") {
			into '/var/lib/ambari-server/resources/stacks/HDP/2.2/services'
		}
	}
}

project('phd') {
	if (tasks.findByPath("artifactoryPublish")) {
		artifactoryPublish {
			properties = ['qa.level': 'basic', 'q.os': 'win32']
		}
	}
	buildRpm {
		packageName = 'scdf-plugin-phd'
		from("${rootProject.projectDir}/src/main/resources/services") {
			into '/var/lib/ambari-server/resources/stacks/PHD/3.0/services'
		}
	}
}

