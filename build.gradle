buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.netflix.nebula:gradle-ospackage-plugin:2.2.6'
	}
}

configure(allprojects) {
	group = 'scdf'
	ext.classifier = '1.noarch'
}

configure(subprojects) { subproject ->
	apply plugin: 'os-package'
	apply plugin: 'rpm'
	buildDeb.enabled = false

	ospackage {
		release = 1
		arch = NOARCH
		os = LINUX
		user 'root'
		permissionGroup = 'root'
		packageDescription = 'Spring Cloud Dataflow plugin contains Ambari\'s stack definition for Spring Cloud Dataflow service.'
		packageGroup = 'Applications/Databases'
		license = 'Apache License v2.0'
		requires('ambari-server', '1.7', GREATER | EQUAL)
		requires('python', '2.6', GREATER | EQUAL)
		postInstall file("${rootProject.projectDir}/src/main/package/rpm/postinstall.sh")
		preUninstall file("${rootProject.projectDir}/src/main/package/rpm/preremove.sh")
	}
	artifacts {
		archives buildRpm
	}
}

project('hdp') {
	buildRpm {
		packageName = 'scdf-plugin-hdp'
		from("${rootProject.projectDir}/src/main/resources/services") {
			into '/var/lib/ambari-server/resources/stacks/HDP/2.2/services'
		}
	}
}

project('phd') {
	buildRpm {
		packageName = 'scdf-plugin-phd'
		from("${rootProject.projectDir}/src/main/resources/services") {
			into '/var/lib/ambari-server/resources/stacks/PHD/3.0/services'
		}
	}
}

